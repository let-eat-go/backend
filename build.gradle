plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.epages.restdocs-api-spec' version '0.18.4'
}

allprojects {
    group = 'com.leteatgo'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = '17'
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.epages.restdocs-api-spec'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // restDocs & swagger
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
        testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.4'
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy 'openapi3'
    }

    openapi3 {
        server 'http://localhost:8080'
        title 'restdocs-swagger Test API Documentation'
        description 'Spring REST Docs with SwaggerUI'
        version '0.0.1'
        format 'json'
        outputDirectory 'src/main/resources/static/docs'
        outputFileNamePrefix "${project.name}"

        project.mkdir "$project.projectDir/$outputDirectory"
    }
}

project(':leteatgo-domain') {
    bootJar { enabled = false }
    jar { enabled = true }
}

project(':leteatgo-global') {
    bootJar { enabled = false }
    jar { enabled = true }
}

project(':leteatgo-external') {
    bootJar { enabled = false }
    jar { enabled = true }
}

project(':leteatgo-api') {
    dependencies {
        implementation project(':leteatgo-domain')
        implementation project(':leteatgo-external')
        implementation project(':leteatgo-global')
    }
}

project(':leteatgo-notification') {
    dependencies {
        implementation project(':leteatgo-domain')
        implementation project(':leteatgo-global')
    }
}

project(':leteatgo-chat') {
    dependencies {
        implementation project(':leteatgo-domain')
        implementation project(':leteatgo-global')
    }
}

project(':leteatgo-batch') {
    dependencies {
        implementation project(':leteatgo-domain')
        implementation project(':leteatgo-global')
    }
}
